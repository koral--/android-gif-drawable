plugins {
	id 'com.jfrog.bintray' version '1.7.3'
}

apply plugin: 'com.android.library'
apply plugin: 'jacoco-android'
apply from: 'gradle-mvn-push.gradle'

version = VERSION_NAME
group = GROUP

android {
	compileSdkVersion versions.compileSdk
	buildToolsVersion versions.buildTools

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	defaultConfig {
		versionName project.version
		minSdkVersion versions.minSdk
		targetSdkVersion versions.targetSdk
		consumerProguardFiles 'consumer-proguard-rules.pro'
		testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
		externalNativeBuild {
			ndkBuild {
				if (isDebugBuild()) {
					cFlags '-DDEBUG'
					arguments 'NDK_DEBUG=1'
				} else {
					cFlags '-DNDEBUG', '-Os', '-g0', '-fvisibility=hidden'
					arguments 'NDK_DEBUG=0'
				}
			}
		}
	}
	externalNativeBuild {
		ndkBuild {
			path 'src/main/c/Android.mk'
		}
	}
	testBuildType 'release'
	signingConfigs {
		release {
			storeFile new File("$System.env.HOME/.android/debug.keystore")
			storePassword 'android'
			keyAlias 'androiddebugkey'
			keyPassword 'android'
		}
	}
	buildTypes {
		release {
			signingConfig signingConfigs.release
		}
	}
}

dependencies {
	provided 'com.android.support:support-annotations:25.1.1'
	testCompile 'junit:junit:4.12'
	testCompile 'org.mockito:mockito-core:2.7.7'
	testCompile 'org.robolectric:robolectric:3.2.2'
	testCompile 'org.assertj:assertj-core:1.7.1'
	testCompile 'net.jodah:concurrentunit:0.4.2'
	testCompile 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
	androidTestCompile 'junit:junit:4.12'
	androidTestCompile 'org.assertj:assertj-core:1.7.1'
	androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.5.0'
	androidTestCompile 'com.android.support:support-annotations:25.1.1'
	androidTestCompile 'com.android.support.test:runner:0.5'
	androidTestCompile 'com.android.support.test:rules:0.5'
}

jacocoAndroidUnitTestReport {
	html.enabled true
	xml.enabled true
}

bintray {
	user = POM_DEVELOPER_ID
	key = BINTRAY_API_KEY
	pkg {
		repo = 'maven'
		name = "$group:$POM_ARTIFACT_ID"
		licenses = ['MIT']
	}
	configurations = ['archives']
}

def isDebugBuild() {
	!gradle.startParameter.taskNames.contains("uploadArchives") && VERSION_NAME.contains("SNAPSHOT")
}

task archiveSymbols(type: Tar) {
	from new File(buildDir, 'intermediates/ndkBuild/release/obj/local')
	baseName = "$POM_ARTIFACT_ID-symbols"
	destinationDir = new File(buildDir, 'libs')
	extension = 'tar.gz'
	compression = Compression.GZIP
}